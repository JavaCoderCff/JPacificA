plugins {
    id 'java'
    id 'maven-publish'
    id 'org.jreleaser' version '1.13.1'
}

group = 'io.github.bybyset'
version = '1.0.0'

allprojects {
    repositories {
        maven {
            url 'https://maven.aliyun.com/repository/public/'
        }
        maven {
            url 'https://maven.aliyun.com/repository/central'
        }
        mavenLocal()
        mavenCentral()
    }
    ext {
        protobuf_version = '3.25.2'
        slf4j_version = '2.0.11'
        guava_version = '33.0.0-jre'
        jctools_version = '4.0.3'
        metrics_version = '4.2.25'
        commons_io_version = '2.16.0'
        disruptor_version = '3.3.7'
        mockito_version = '4.11.0'
        jupiter_version = '5.11.0-M2'
    }
}

subprojects {
    apply plugin: 'java'
    sourceCompatibility = 8
    targetCompatibility = 8

    dependencies {
        implementation "org.slf4j:slf4j-api:$slf4j_version"

        testImplementation "org.junit.jupiter:junit-jupiter:$jupiter_version"
        testImplementation "org.junit.jupiter:junit-jupiter-api:$jupiter_version"
        testImplementation "org.junit.jupiter:junit-jupiter-params:$jupiter_version"
        testImplementation "org.junit.jupiter:junit-jupiter-engine:$jupiter_version"
    }

}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    repositories {
        maven {
            name = "Snapshots"
            url = "https://aws.oss.sonatype.org/content/repositories/snapshots"
            credentials {
                username "$System.env.SONATYPE_USERNAME"
                password "$System.env.SONATYPE_PASSWORD"
            }
        }
    }
    publications {
        pluginZip(MavenPublication) { publication ->
            pom {
                name = "jpacifica"
                description = "java implementation of PacificA"
                groupId = "io.github.bybyset"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        name = "bybyset"
                        url = "https://github.com/bybyset/jpacifica"
                    }
                }
            }
        }
    }
}

jreleaser {
    signing {
        active = 'ALWAYS'
        armored = true
    }
    deploy {
        maven {
            nexus2 {
                'maven-central' {
                    active = 'ALWAYS'
                    url = 'https://s01.oss.sonatype.org/service/local'
                    snapshotUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
                    closeRepository = true
                    releaseRepository = true
                    stagingRepository('build/staging-deploy')
                }
            }
            /* Portal Publisher API
            mavenCentral {
                sonatype {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('target/staging-deploy')
                }
            }
            */
        }
    }
}